### BNF Rules
digit		= '0' | ... | '9'  
constant	= digit { digit }  
variable	= 'a' | ... | 'z'  
factor		= constant | variable | '('  expression  ')'  
term		= factor  { ('\*'|'/') factor }  
expression	= term  { ('+' | '-') term }  
equality	= expression '=' expression  

### Prod. Rules
0. D 	-> n
1. C' 	-> ¤
2. C' 	-> D C'
3. C 	-> D C'
4. V 	-> a
5. F 	-> C
6. F 	-> V
7. F 	-> '(' E ')'
8. T 	-> F T'
9. T' 	-> ¤
10. T' 	-> '\*' T
11. T' 	-> '/' T
12. E 	-> T E'
13. E' 	-> ¤
14. E' 	-> '+' E
15. E' 	-> '-' E
16. S 	-> E '=' E

### First sets
Fi(D) 	= { n }  
Fi(C')	= Fi(D) U { ¤ } = { n , ¤ }  
Fi(C)	= Fi(D) = { n }  
Fi(V)	= { a }  
Fi(F)	= Fi(C) U Fi(V) U { '(' } = { n , a, '(' }  
Fi(T)	= Fi(F) = { n , a, '(' }  
Fi(T')	= { '\*', '/', ¤ }  
Fi(E)	= Fi(T) = { n , a, '(' }  
Fi(E')	= {'+', '-', ¤ }  
Fi(S)	= Fi(E) = { n , a, '(' }  

### Follow sets
Fo(D) 	= { Fi(C') - ¤ } U Fo(C') = { n, '\*', '/', '+', '-', ')', '=', $ }  
Fo(C')	= Fo(C) = { '\*', '/', '+', '-', ')', '=', $ }  
Fo(C)	= Fo(F) = { '\*', '/', '+', '-', ')', '=', $ }  
Fo(V)	= Fo(F) = { '\*', '/', '+', '-', ')', '=', $ }  
Fo(F)	= { Fi(T') - ¤ } U Fo(T) = { '\*', '/', '+', '-', ')', '=', $ }  
Fo(T)	= { Fi(E') - ¤ } U Fo(T') = { '+', '-', ')', '=', $ }  
Fo(T')	= Fo(T) = { '+', '-', ')', '=', $ }  
Fo(E)	= { ')', '=' } U Fo(E') U Fo(S) = { ')', '=', $ }  
Fo(E')	= Fo(E) = { ')', '=', $ }  
Fo(S)	= { $ }  

### LL1 table 

|Rule|'0' ... '9'|'a' ... 'z'|'='|'('|')'|'+'|'-'|'\*'|'/'|null|
|:--:|:---------:|:---------:|:-:|:-:|:-:|:-:|:-:|:--:|:-:|:--:|
| D  |     0     |           |   |   |   |   |   |    |   |    |
| C' |     2     |           | 1 |   | 1 | 1 | 1 |  1 | 1 |  1 |
| C  |     3     |           |   |   |   |   |   |    |   |    |
| V  |           |     4     |   |   |   |   |   |    |   |    |
| F  |     5     |     6     |   | 7 |   |   |   |    |   |    |
| T  |     8     |     8     |   | 8 |   |   |   |    |   |    |
| T' |           |           | 9 |   | 9 | 9 | 9 | 10 |11 |  9 |
| E  |    12     |    12     |   |12 |   |   |   |    |   |    |
| E' |           |           |13 |   |13 |14 |15 |    |   | 13 |
| S  |    16     |    16     |   |16 |   |   |   |    |   |    |
